import algebra.category.Module.basic
import data.matrix.basic -- we're going to do matrices
import data.real.basic -- with coefficients in the real numbers
import linear_algebra.tensor_product

/-!

# Picard group of a commutative ring done with categories

A group is a set with some structure (in Lean we say
a type with some structure). A category is 
just the same -- it's a type with some structure.

## Definition of a category

A category is a type `C` equipped with the following
extra structure:

* For every pair of terms `x y : C`, a set `x ‚ü∂ y` 
  of "abstract arrows from `x` to `y`". Note that 
  this is *not* the usual `\r` implication arrow;
  this is a weird new arrow `\hom`.

* For every `x : C`, a special abstract arrow `ùüô x : x ‚ü∂ x`
  called "the identity map" by mathematicians and `id` by Lean.

* For every `x y z : C` and every pair of abstract
arrows `f : x ‚ü∂ y` and `g : y ‚ü∂ z`, an abstract 
"composition arrow" `f ‚â´ g : x ‚ü∂ z`, called `comp` by Lean.

* Such that the axioms `id_comp`, `comp_id` and `comp_assoc`
  are satisfied. Exercise: guess what these axioms say.

  Exercise: look up the axioms of a monoid. Is
  a category a monoid?
-/

universe u

open category_theory

variables (C : Type u) [category C] (x y z : C)
  (f : x ‚ü∂ y) (g : y ‚ü∂ z)

--#check ùüô x -- x ‚ü∂ x
--#check f ‚â´ g -- x ‚ü∂ z

/-

# Example of a category

First I need to tell you the type for the category,
and the type is the natural numbers.

Next, given two natural numbers `m` and `n`, I
define the set `m ‚ü∂ n` to be the `n` by `m` matrices with real
coefficients.

We continue with the structure we need to make this
into a category. We need to define the identity arrow
`n ‚ü∂ n` if `n` is a natural number, and of course we use
the `n √ó n` identity matrix.

Finally we need to define composition of arrows.
We do this as follows. If `A : m ‚Üí n` is an `n √ó m`
matrix and and `B : n ‚Üí p` is a `p √ó n` matrix,
we define `A ‚â´ B` to be the `p √ó n` matrix `B * A`.

Can you check to see that this gives the natural numbers
the structure of a category?

-/

example : category ‚Ñï :=
{ hom := Œª m n, matrix (fin n) (fin m) ‚Ñù,
  id := Œª n, (1 : matrix (fin n) (fin n) ‚Ñù),
  comp := Œª m n p A B, matrix.mul B A,
  id_comp' := begin
    sorry
  end,
  comp_id' := begin
    sorry
  end,
  assoc' := begin
    sorry
  end }

/- 

Here's another approach. Why don't we define the
abstract arrows not to be matrix rings but to be
actual linear maps -- "a different way of looking
at the same thing".

-/

example : category ‚Ñï :=
{ hom := Œª m n, (fin m ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] (fin n ‚Üí ‚Ñù),
  id := Œª n, linear_map.id,
  -- comp comes out backwards because the traditional
  -- notation for writing functions is on the left
  -- `f(x)`.
  comp := Œª m n p hmn hnp, linear_map.comp hnp hmn,
  id_comp' := begin
    sorry,
  end,
  comp_id' := begin
    sorry,
  end,
  assoc' := begin
    sorry,
  end }

-- 
/-

If `R` is a ring in universe `u`, then
`Module.{u u} R` (hitherto abbreviated to `Module R`)
is the type of `R`-modules in universe `u`.

Unsurprisingly, if `M N : Module R` then
the opaque arrow type `M ‚ü∂ N` is just defined to mean
`M ‚Üí‚Çó[R] N`, the type of `R`-linear maps from `M` to `N`.

## Isomorphisms in categories

An *isomorphism* `x ‚âÖ y` in a category is a pair
of arrows `to_fun : x ‚ü∂ y` and `inv_fun : y ‚ü∂ x`
such that `to_fun ‚â´ inv_fun = ùüô _` and `inv_fun ‚â´ to_fun = ùüô _`

For example if `m` and `n` are natural numbers in our
example category, there are no isomorphisms `m ‚âÖ n` at all
if `m ‚â† n`, but there are many isomorphisms `n ‚âÖ n`, one
for every invertible real `n √ó n` matrix.

Let's define the "we are isomorphic" equivalence relation
on the elements of our category. Hint: the key construction
for the `refl` proof is `iso.refl`. What do you think the
key constructions for `symm` and `trans` will be?

-/

/-- the setoid structure for being isomorphic in a category -/
def category_theory.s : setoid C :=
{ r := Œª M N, nonempty (M ‚âÖ N),
  iseqv := begin
    sorry,
  end }

/-

# Quotient categories

Sometimes we put equivalence relations on groups.
For example if `H : subgroup G` then we can partition
`G` into the left cosets `gH` of `H`, and the equivalence
relation associated to the partition is `g‚ÇÅ ‚âà g‚ÇÇ ‚Üî g‚ÇÅ‚Åª¬π * g‚ÇÇ ‚àà H`.
We can also partition `G` into the right cosets of `H`
and get a different (in general) equivalence relation.

If you have a group with an equivalence relation on it,
then you can quotient out the group by the equivalence
relation to get a quotient type, and then we can ask
the question of whether the multiplication on the group "descends"
(or as Lean calls it, "lifts" :-( ) to a multiplication
on the quotient, giving us what mathematicians call a
quotient group.

This "descent of multiplication" is not automatic. For example
there is in general no natural multiplication on the set of
left cosets of a group by a subgroup. The same is true
for the right cosets. If however the subgroup `H`
is *normal*, then the left cosets and the right cosets
coincide, the two equivalence relations become equal,
and this is the key thing needed to make the
calculation work; the multiplication descends and we get
a quotient group. 

The axiom on an equivalence relation on a group
which is necessary and sufficient for the multiplication
to descend to the quotient is called "being a congruence
relation". Congruence relations in Lean are called `con`.
They were developed by Amelia Livingston as part of her work with me
defining schemes in Lean.

-/

/-

# The category of R-modules

This is `bundled_modules R` from last time. It's the
type of all `R`-modules.

-/

variables (R : Type u) [comm_ring R]

#check Module.{u u} R -- Type (u+1)

/-

Ooh -- a universe bump!

The "set of all real vector spaces" isn't a set because
of stupid reasons to do with sets being elements of themselves.
Mathematicians often say "it's too big to be a set". What's
going on is that it is a type, but in the next universe up.

-/

open category_theory

def Module.one : Module.{u u} R := Module.of R R

instance : has_one (Module.{u u} R) :=
{ one := Module.one R }

open_locale tensor_product

def Module.mul (M N : Module.{u u} R) : Module.{u u} R :=
Module.of R (M ‚äó[R] N)

/-- Activate notation `*` for R-modules (it's tensor product).  -/
instance : has_mul (Module.{u u} R) :=
{ mul := Module.mul R }

-- impossible to make in Lean
-- example : monoid (Module.{u u} R) :=
-- { mul := (*),
--   one := 1,
--   mul_assoc := begin
--     intros M N P,
--     -- aargh this isn't true
--     -- (M ‚äó N) ‚äó P and M ‚äó (N ‚äó P) are
--     -- isomorphic but might not be *equal*
--     sorry
--   end,
--   one_mul := sorry, -- probably not true
--   mul_one := sorry, -- probably not true
-- }

open Module

def Module.category.con : con (Module.{u u} R) :=
{ mul' := begin
    rintro M N P Q ‚ü®eMN‚ü© ‚ü®ePR‚ü©,
    change nonempty (of R (M ‚äó[R] P) ‚âÖ of R (N ‚äó[R] Q)),
    replace eMN := eMN.to_linear_equiv,
    replace ePR := ePR.to_linear_equiv,
    exact ‚ü®{ 
      hom := tensor_product.map eMN ePR,
      inv := tensor_product.map eMN.symm ePR.symm,
      hom_inv_id' := begin
        convert (tensor_product.map_comp _ _ _ _).symm,
        simp,
        refl, 
      end,
      inv_hom_id' := begin
        convert (tensor_product.map_comp _ _ _ _).symm,
        simp,
        refl,
      end }‚ü©,
  end,
  ..category_theory.s (Module.{u u} R) }

instance : setoid (Module.{u u} R) := (Module.category.con R).to_setoid 

abbreviation Module.Picard_monoid := (Module.category.con R).quotient

-- We can't get an induced monoid structure on `Module.Picard_monoid`
-- from some `monoid (Module.{u u} R)` term, because there is no
-- such natural term. We will have to make this ourselves
instance : monoid (Module.Picard_monoid R) :=
{ mul := (*),
  mul_assoc := begin
    intros a b c,
    apply quotient.induction_on‚ÇÉ a b c, clear a b c,
    intros M N P,
    suffices : ‚ü¶(M * N) * P‚üß = ‚ü¶M * (N * P)‚üß,
    { simpa, },
    apply quotient.sound,
    apply nonempty.map (linear_equiv.to_Module_iso'),
    use tensor_product.assoc R M N P,
  end,
  one := ‚ü¶1‚üß,
  one_mul := begin
    intro a,
    apply quotient.induction_on a, clear a,
    intro M,
    suffices : ‚ü¶1 * M‚üß = ‚ü¶M‚üß,
    { simpa },
    apply quotient.sound,
    apply nonempty.map (linear_equiv.to_Module_iso'),
    use tensor_product.lid R M,
  end,
  mul_one := begin
    intro a,
    apply quotient.induction_on a, clear a,
    intro M,
    suffices : ‚ü¶M * 1‚üß = ‚ü¶M‚üß,
    { simpa },
    apply quotient.sound,
    apply nonempty.map (linear_equiv.to_Module_iso'),
    use tensor_product.rid R M,
  end,
}

/-- The Picard group of a ring (defined via modules) -/
abbreviation Module.Picard_group := units (Module.Picard_monoid R)

-- doesn't trigger automatically because of the R parameter I guess?
instance : group (Module.Picard_group R) := units.group

-- we lost a universe level!
#check Module.Picard_group R -- Type (u + 1)

-- It's a group, but it's in a universe one higher than where we started
-- because we went via the "class" of all R-modules. 

